PREFIX sio: <http://semanticscience.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX bao: <http://www.bioassayontology.org/bao#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pubchem: <http://rdf.ncbi.nlm.nih.gov/pubchem/>

SELECT DISTINCT ?upProt ?assay ?assay_type ?outcome ?compound_cid ?ref_cit ?compound_name ?SMILES ?InChI (COUNT(DISTINCT ?target) AS ?target_count) WHERE {
  	#consider only endpoints with = value or implicitly equal
	VALUES ?bannedQualifier {obo:GENEPIO_0001002 obo:GENEPIO_0001003 obo:GENEPIO_0001005 obo:GENEPIO_0001006 }
    VALUES ?outcome {vocab:active vocab:inactive}
	VALUES ?type {bao:BAO_0000034 bao:BAO_0000186 bao:BAO_0000187 bao:BAO_0000188 bao:BAO_0000190 bao:BAO_0000192 bao:BAO_0002146}
	VALUES ?upProt {$protein_list}
	GRAPH pubchem:protein {
		?prot skos:closeMatch ?upProt
  	}
  	GRAPH pubchem:measuregroup{
    	?mg obo:RO_0000057 ?prot;
    		obo:RO_0000057 ?target
  	}
  
  	?mg obo:OBI_0000299 ?ep ;
    	^bao:BAO_0000209 ?assay .
	#only confirmatory assay
	?assay bao:BAO_0000540 ?assay2 .
	
  
  	?ep rdf:type ?assay_type ;
		vocab:PubChemAssayOutcome ?outcome ;
  		obo:IAO_0000136 [cheminf:CHEMINF_000477 ?compound_cid] ;
                     
	FILTER NOT EXISTS {?ep vocab:hasQualifier ?bannedQualifier}
  
	OPTIONAL {
    	#identifiers badly defined by PubChem, consider citation only
    	?ep cito:citesAsDataSource [dcterms:bibliographicCitation ?ref_cit].
  	}
	OPTIONAL{
    	?compound_cid rdfs:label ?compound_name
  	}
  	OPTIONAL{
    	?compound_cid sio:SIO_000008 [ rdf:type sio:CHEMINF_000376;
                           sio:SIO_000300 ?SMILES]
  	}
	OPTIONAL{
    	?compound_cid sio:SIO_000008 [ rdf:type sio:CHEMINF_000396;
                           sio:SIO_000300 ?InChI]
  	}
  	#other Compound mappings badly defined ind PubChem
} GROUP BY ?upProt ?assay_type ?outcome ?compound_cid ?ref_cit ?compound_name ?SMILES ?InChI ?assay